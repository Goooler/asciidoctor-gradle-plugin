apply plugin: 'org.ysb33r.gradletest'

pluginManager.withPlugin('org.ysb33r.cloudci') {
    ci {
        travisci {
            gradleTest {
                if (JavaVersion.current().java11) {
                    versions '4.8.1', '4.10.2', '5.0', '5.3-rc-3'
                } else {
                    versions '4.0.2', '4.6', '5.1.1'
                }
            }
        }
        appveyor {
            gradleTest {
                versions '4.0.2', '4.10.2', '5.0', '5.1.1'
            }
        }
        no_ci {
            gradleTest {
                versions '4.10.2', '5.0', '5.1.1', '5.3-rc-3'

                if (JavaVersion.current().java11) {
                    versions '4.8.1'
                } else if (JavaVersion.current().java10) {
                    versions '4.7'
                } else {
                    versions '4.0.2', '4.6'
                }
            }
        }
    }
}

gradleTest {
    if (tasks.findByName('intTest')) {
        mustRunAfter 'intTest'
    }

    enabled = !gradle.startParameter.offline

    dependsOn ':testfixtures-offline-repo:buildOfflineRepositories'
    systemProperties OFFLINE_REPO: offlineRepoRoot.absolutePath
}


licenseGradleTest.enabled = false

pluginManager.withPlugin('jacoco') {
    jacocoTestReport {
        executionData gradleTest
        mustRunAfter gradleTest
    }
}
