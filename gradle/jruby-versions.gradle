ext {
    asciidoctorjExtFileContent = file("${project(':asciidoctor-gradle-jvm').projectDir}/src/main/groovy/org/asciidoctor/gradle/jvm/AsciidoctorJExtension.groovy").readLines()
    jrubyFixtureFileContent = file("${project(':testfixtures-jvm').projectDir}/src/main/groovy/org/asciidoctor/gradle/testfixtures/jvm/JRubyTestVersions.groovy").readLines()

    snakeYamlForJRubyVersion = (asciidoctorjExtFileContent.grep {
        it =~ /final\s+static\s+String\s+PDF_SNAKE_YAML_FOR_EARLY_JRUBY9_VERSIONS\s+=/
    })[0].replaceFirst(~/^(.+?["'])/, '').replaceFirst(~/(["'].*)$/, '')

    readJRubyVersion = { itemName ->
        (jrubyFixtureFileContent.grep {
            it =~ $/final\s+static\s+String\s+${itemName}\s+=/$
        })[0].replaceFirst(~/^(.+?["'])/, '').replaceFirst(~/(["'].*)$/, '')
    }

    jrubyTestVersion = [

        aj15_minimum : readJRubyVersion('AJ15_ABSOLUTE_MINIMUM'),
        aj15_safe_mininum : readJRubyVersion('AJ15_SAFE_MINIMUM'),
        aj15_safe_maximum : readJRubyVersion('AJ15_SAFE_MAXIMUM'),
        aj15_maximum : readJRubyVersion('AJ15_ABSOLUTE_MAXIMUM'),

        aj16_minimum : readJRubyVersion('AJ16_ABSOLUTE_MINIMUM'),
        aj16_safe_mininum : readJRubyVersion('AJ16_SAFE_MINIMUM'),
        aj16_safe_maximum : readJRubyVersion('AJ16_SAFE_MAXIMUM'),
        aj16_maximum : readJRubyVersion('AJ16_ABSOLUTE_MAXIMUM')

    ]
}

jrubyTestVersion.keySet().each {
    String config = "intTestOfflineJRuby_${it}"
    configurations.create(config)
    dependencies.add(config, "org.jruby:jruby-complete:${jrubyTestVersion[it]}") {
        transitive = false
    }
}
